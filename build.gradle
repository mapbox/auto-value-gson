// modified by Mapbox
// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()
        google()
        maven {
            url 'https://api.mapbox.com/downloads/v2/releases/maven'
            authentication {
                basic(BasicAuthentication)
            }
            credentials {
                username = "mapbox"
                password = project.hasProperty('SDK_REGISTRY_TOKEN') ? project.property('SDK_REGISTRY_TOKEN') : System.getenv('SDK_REGISTRY_TOKEN')
            }
        }
    }
    dependencies {
        classpath "net.ltgt.gradle:gradle-apt-plugin:0.19"
        classpath "com.mapbox.gradle.plugins:sdk-registry:0.7.0"
    }
}

allprojects { project ->
    project.repositories {
        maven {
            url 'http://oss.sonatype.org/content/repositories/snapshots'
        }
        mavenCentral()
    }

    project.plugins.withId("net.ltgt.apt") {
        try {
            // automatically apply net.ltgt.apt-idea whenever net.ltgt.apt is used
            project.apply plugin: "net.ltgt.apt-idea"
            // disable addAptDependencies (if you delegate build actions to Gradle)
            project.plugins.withType(JavaPlugin) {
                project.afterEvaluate {
                    project.idea.module.apt.addAptDependencies = false
                }
            }
        } catch (ignored) {
            // ignore, in case an older version of net.ltgt.apt is being used
            // that doesn't come with net.ltgt.apt-idea.
        }
    }
}

ext.deps = [
    autotransient: 'io.sweers.autotransient:autotransient:1.0.0',
    errorProneAnnotations: 'com.google.errorprone:error_prone_annotations:2.3.1',
    jetbrainsAnnotations: 'org.jetbrains:annotations-java5:16.0.2',
    gson: 'com.google.code.gson:gson:2.8.5'
]

