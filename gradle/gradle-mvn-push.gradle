// modified by mapbox
/*
 * Copyright 2013 Chris Banes
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'com.mapbox.sdkRegistry'

version = VERSION_NAME
group = GROUP

publishing {
    publications {
        MapboxLibraryPublication(MavenPublication) {
            groupId GROUP
            version VERSION_NAME
            artifact("build/libs/${POM_ARTIFACT_ID}-${VERSION_NAME}.jar")

            pom {
                name.set(POM_NAME)
                description.set(POM_DESCRIPTION)
                url.set(POM_URL)

                licenses {
                    license {
                        name.set(POM_LICENCE_NAME)
                        url.set(POM_LICENCE_URL)
                        distribution.set(POM_LICENCE_DIST)
                    }
                }

                scm {
                    url.set(POM_SCM_URL)
                    connection.set(POM_SCM_CONNECTION)
                    developerConnection.set(POM_SCM_DEV_CONNECTION)
                }

                developers {
                    developer {
                        id.set(POM_DEVELOPER_ID)
                        name.set(POM_DEVELOPER_NAME)
                    }
                }

                withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    // adding dependency manually because `from components.java`, which is supposed
                    // to add api dependencies, doesn't work due to showjar configuration
                    configurations.api.allDependencies.each { dep ->
                        if (dep.group && dep.name && dep.version) {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', dep.group)
                            dependencyNode.appendNode('artifactId', dep.name)
                            dependencyNode.appendNode('version', dep.version)
                            dependencyNode.appendNode('scope', 'compile')
                        }
                    }
                }
            }
        }
    }
    repositories {
        mavenLocal()
    }
}

registry {
    sdkName = "com-mapbox-mapboxsdk-" + project.name
    production = true
    snapshot = VERSION_NAME.endsWith("-SNAPSHOT")
    override = snapshot
    dryRun = false
    publish = true
    publications = ["MapboxLibraryPublication"]
}